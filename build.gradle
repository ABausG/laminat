buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath "org.apache.commons:commons-lang3:$commonsLang3Version"
    classpath 'org.apache.commons:commons-text:1.1'
  }
}

plugins {
  id 'nebula.kotlin' version '1.2.41'
  id 'org.jmailen.kotlinter' version '1.11.2'
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC6-4'
  id 'org.jetbrains.dokka' version '0.9.17'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

import org.apache.commons.text.StringEscapeUtils

String buildName(Project project) {
  if (project.name.startsWith('pact-jvm')) {
    project.name
  } else {
    if (project.parent) {
      buildName(project.parent) + '-' + project.name
    } else {
      project.name
    }
  }
}

subprojects {
  buildscript {
    repositories {
      jcenter()
      mavenCentral()
      mavenLocal()
    }
  }

  project.ext {
    scalaVersion = '2.12'
    scalaFullVersion = '2.12.5'
  }

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'codenarc'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'io.gitlab.arturbosch.detekt'
//    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
      compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
      compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
      compile "org.slf4j:slf4j-api:${project.slf4jVersion}"
      compile('io.github.microutils:kotlin-logging:1.4.4') {
        exclude group: 'org.jetbrains.kotlin'
      }

      testCompile 'org.hamcrest:hamcrest-all:1.3',
        'org.mockito:mockito-core:1.10.19',
        "junit:junit:${project.junitVersion}"

      testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
      }
      testCompile "cglib:cglib:${project.cglibVersion}"
      testCompile 'org.objenesis:objenesis:2.6'
      testCompile 'io.kotlintest:kotlintest:2.0.7'
    }

    group = 'au.com.dius'
    version = '4.0.0-beta.0'
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.additionalParameters = ['-target:jvm-1.8']
        configure(scalaCompileOptions.forkOptions) {
          memoryMaximumSize = '256m'
        }
    }

    compileScala {
      classpath = classpath.plus(files(compileGroovy.destinationDir)).plus(files(compileKotlin.destinationDir))
      dependsOn compileGroovy, compileKotlin
    }

    compileTestGroovy {
      classpath = classpath.plus(files(compileTestKotlin.destinationDir))
      dependsOn compileTestKotlin
    }

    tasks.withType(GroovyCompile) {
      groovyOptions.optimizationOptions.indy = true
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
      kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
      }
    }

//    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
//      outputFormat = 'javadoc'
//      outputDirectory = "$buildDir/docs/dokkaJavadoc"
//    }

    test {
      systemProperty 'pact.rootDir', "$buildDir/pacts"
    }

    jar {
        manifest.attributes provider: 'gradle',
                'Implementation-Title': project.name, 'Implementation-Version': version,
                'Implementation-Vendor': project.group, 'Implementation-Vendor-Id': project.group,
                'Specification-Vendor': project.group,
                'Specification-Title': project.name,
                'Specification-Version': version
    }

    task javadocJar(type: Jar, dependsOn: [javadoc, groovydoc/*, dokkaJavadoc*/]) {
        classifier = 'javadoc'
      if (project.hasProperty('scalaVersion')) {
        from javadoc.destinationDir, scaladoc.destinationDir, groovydoc.destinationDir //, dokkaJavadoc.outputDirectory
        dependsOn << scaladoc
      } else {
        from javadoc.destinationDir, groovydoc.destinationDir //, dokkaJavadoc.outputDirectory
      }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    def pomCustomisation = {
        name project.name
        description StringEscapeUtils.escapeXml11(new File(projectDir, 'README.md').text)
        url 'https://github.com/DiUS/pact-jvm'
        licenses {
            license {
                name 'Apache 2'
	            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	            distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/DiUS/pact-jvm'
            connection 'https://github.com/DiUS/pact-jvm.git'
        }

	    developers {
            developer {
                id 'thetrav'
                name 'Travis Dixon'
                email 'the.trav@gmail.com'
            }
            developer {
                id 'rholshausen'
                name 'Ronald Holshausen'
                email 'rholshausen@dius.com.au'
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                beforeDeployment { def deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    if (project.hasProperty('sonatypeUsername')) {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                }

            }
        }
    }

    ext.installer = install.repositories.mavenInstaller
    ext.deployer = uploadArchives.repositories.mavenDeployer
    installer.pom.project(pomCustomisation)
    deployer.pom.project(pomCustomisation)

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") || gradle.taskGraph.hasTask("publishPlugins") }
        sign configurations.archives
    }

    codenarcMain {
        configFile = file("${project.rootDir}/config/codenarc/ruleset.groovy")
    }

    codenarcTest {
        configFile = file("${project.rootDir}/config/codenarc/rulesetTest.groovy")
    }

    check.dependsOn << 'jacocoTestReport'

  kotlinter {
    indentSize = 2
    continuationIndentSize = 2
    reporters = ['checkstyle', 'plain', 'json']
  }

  detekt {
    profile("main") {
      config = "${project.rootDir}/config/detekt-config.yml"
      if (file("$projectDir/src/main/kotlin").exists()) {
        input = "$projectDir/src/main/kotlin"
      }
    }
    profile("test") {
      config = "${project.rootDir}/config/detekt-config-test.yml"
      if (file("$projectDir/src/test/kotlin").exists()) {
        input = "$projectDir/src/test/kotlin"
      }
    }
  }

  check.dependsOn << 'detektCheck'

  task allDeps(type: DependencyReportTask) {}
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
//            maxParallelForks = 2
            minHeapSize = '128m'
            maxHeapSize = '768m'
        }
    }
}
