project.ext {
    scalaVersion = '2.10'
    sbtVersion = '0.13.5'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.5', 'org.scala-lang:scala-library:2.10.3',
                "com.typesafe.scala-logging:scala-logging-slf4j_${project.scalaVersion}:2.1.2",
                "net.databinder:unfiltered-netty-server_${project.scalaVersion}:0.7.1",
                "net.databinder.dispatch:dispatch-core_${project.scalaVersion}:0.11.0",
                "org.json4s:json4s-native_${project.scalaVersion}:3.2.6",
                "org.json4s:json4s-jackson_${project.scalaVersion}:3.2.6"
        testCompile 'junit:junit:4.11', 'org.mockito:mockito-all:1.9.5', "org.specs2:specs2_${project.scalaVersion}:2.2.3"
    }

    group = 'au.com.dius'
    version = '2.0-RC3'
    targetCompatibility = '1.6'
    sourceCompatibility = '1.6'

    jar {
        manifest.attributes provider: 'gradle',
                'Implementation-Title': project.name, 'Implementation-Version': version,
                'Implementation-Vendor': project.group, 'Implementation-Vendor-Id': project.group,
                'Specification-Vendor': project.group,
                'Specification-Title': project.name,
                'Specification-Version': version
    }

    task javadocJar(type: Jar, dependsOn: [javadoc, scaladoc]) {
        classifier = 'javadoc'
        from javadoc.destinationDir, scaladoc.destinationDir
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    def pomCustomisation = {
        url 'https://github.com/DiUS/pact-jvm'
        licenses {
            license {
                name 'Apache 2'
	            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	            distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/DiUS/pact-jvm'
            connection 'https://github.com/DiUS/pact-jvm.git'
        }

	    developers {
            developer {
                id 'thetrav'
                name 'Travis Dixon'
                email 'the.trav@gmail.com'
            }
            developer {
                id 'rholshausen'
                name 'Ronald Holshausen'
                email 'rholshausen@dius.com.au'
            }
            developer {
                id 'kenbot'
                name 'Ken Scambler'
                email 'ken.scambler@gmail.com'
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {

            }
        }
    }

    ext.installer = install.repositories.mavenInstaller
    ext.deployer = uploadArchives.repositories.mavenDeployer
    installer.pom.project(pomCustomisation)
    deployer.pom.project(pomCustomisation)
}
