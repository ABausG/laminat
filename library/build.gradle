apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName rootProject.ext.pact_version
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "com.squareup.okhttp3:mockwebserver:$mockserver_version"

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "org.apache.commons:commons-lang3:$apache_commons_version"
    implementation "org.apache.commons:commons-collections4:$apache_collections_version"
    implementation "org.apache.httpcomponents:httpcore:$apache_httpcore_version"
    implementation "joda-time:joda-time:$jodatime_version"
    implementation "com.github.mifmif:generex:$generex_version"
    implementation "io.gatling:jsonpath_2.11:$jsonpath_version"
    implementation "com.github.zafarkhaja:java-semver:$semver_version"

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'maven-publish'

def coreAarFile = file('build/outputs/aar/library-release.aar')
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing.publications {
    broker(MavenPublication) {
        artifact coreAarFile
        groupId "com.github.janphkre"
        artifactId "laminat-android"
        version "$pact_version"
        //The publication doesn't know about our dependencies, so we have to manually add them to the pom
        pom.withXml {
            def dependenciesNode = asNode().appendNode('dependencies')
            consumeNodes(dependenciesNode, configurations.api.allDependencies)
        }
    }
}

static def consumeNodes(Node dependenciesNode, DependencySet dependencySet) {
    //Iterate over the dependencies (we don't want the test ones), adding a <dependency> node for each
    dependencySet.each {
        if(it instanceof ExternalModuleDependency) {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
        }
    }
}

publishing.repositories {
    mavenLocal()
}