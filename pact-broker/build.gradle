apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'groovy'

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    extraLibs "org.codehaus.groovy:groovy-all:$groovy_version"

    extraLibs "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    extraLibs "org.apache.commons:commons-lang3:$apache_commons_version"
    extraLibs "org.codehaus.groovy.modules.http-builder:http-builder:$http_builder_version"
    extraLibs "com.github.salomonbrys.kotson:kotson:$kotson_version"
    extraLibs "org.apache.httpcomponents:httpclient:$http_client_version"
    extraLibs "org.apache.commons:commons-collections4:$apache_collections_version"
    extraLibs "com.google.guava:guava:$guava_version"
    configurations.api.extendsFrom(configurations.extraLibs)
}

sourceCompatibility = "7"
targetCompatibility = "7"

if (project.tasks.findByName('compileKotlin')) {
    compileGroovy {
        classpath = classpath.plus(files(compileKotlin.destinationDir))
        dependsOn compileKotlin
    }
}

group = group
version = pact_version
jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes("Implementation-Title": "Laminat pact-broker",
                "Implementation-Version": pact_version,
                "Implementation-Version-Code": pact_version_code)
    }
    baseName = broker_artifact
}
