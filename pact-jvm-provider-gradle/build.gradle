buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.9.3'
  }
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

dependencies {
  compile localGroovy(),
    "org.codehaus.groovy.modules.http-builder:http-builder:$httpBuilderVersion"
  compile(project(":pact-jvm-provider_${project.scalaVersion}")) {
    exclude module: 'groovy-all'
  }
  compile 'org.fusesource.jansi:jansi:1.11'

  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/DiUS/pact-jvm/tree/master/pact-jvm-provider-gradle'
  vcsUrl = 'https://github.com/DiUS/pact-jvm.git'
  description = 'Gradle plugin for verifying pacts against a provider.'
  tags = ['pact', 'provider', 'consumer driven contracts']

  plugins {
    pactProviderPlugin {
      id = 'au.com.dius.pact'
      displayName = 'Gradle Pact Provider plugin'
    }
  }
}

test {

  // exclude the gradle version of jansi from the classpath
  classpath = project.sourceSets.test.runtimeClasspath.filter {
    it.name != 'jansi-1.2.1.jar'
  }

}
