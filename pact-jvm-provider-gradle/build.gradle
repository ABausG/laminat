buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
  }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

dependencies {

    if (project.groovyIndy) {
      compile 'org.codehaus.groovy:groovy-all:2.3.6:indy'
    } else {
      compile 'org.codehaus.groovy:groovy-all:2.3.6'
    }

    compile gradleApi(), project(":pact-jvm-provider_${project.scalaVersion}"),
        'org.fusesource.jansi:jansi:1.11', 'org.codehaus.groovy.modules.http-builder:http-builder:0.7',
        'org.apache.httpcomponents:httpclient:4.3.6', 'com.googlecode.java-diff-utils:diffutils:1.3.0'

    testCompile project(":pact-specification-test_${project.scalaVersion}")
}

tasks.withType(GroovyCompile) {
  groovyOptions.optimizationOptions.indy = project.groovyIndy
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
    }
  }
}

bintray {
  if (project.hasProperty('bintrayUser')) {
    user = bintrayUser
    key = bintrayKey
  }

  publications = ['maven']

  dryRun = false //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    repo = 'maven'
    name = project.name
    desc = file('README.md').text
    websiteUrl = 'https://github.com/DiUS/pact-jvm/tree/master/pact-jvm-provider-gradle'
    issueTrackerUrl = 'https://github.com/DiUS/pact-jvm/issues'
    vcsUrl = 'https://github.com/DiUS/pact-jvm.git'
    licenses = ['Apache-2.0']
    labels = ['pact', 'gradle', 'gradle plugin']
    publicDownloadNumbers = true
    //Optional version descriptor
    version {
      attributes = ['gradle-plugin': "au.com.dius.pact:au.com.dius:${project.name}"]
    }
  }
}
